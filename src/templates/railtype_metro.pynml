// Metro

spriteset (spriteset_metro_gui, "${graphics_path}/railtypes/metro.png") {
    template_gui(10,760)
}

spriteset (spriteset_metro_track_overlay, "${graphics_path}/railtypes/metro.png") {
    template_track_overlay(10,20)
}

spriteset (spriteset_metro_underlay, "${graphics_path}/railtypes/metro.png") {
    template_underlay(10,60)
}

spriteset (spriteset_metro_tunnels, "${graphics_path}/railtypes/metro.png") {
    template_tunnels(330,460)
}

spriteset (spriteset_metro_bridge_surfaces, "${graphics_path}/railtypes/metro.png") {
    template_bridge_surfaces(10,300)
}

spriteset (spriteset_metro_level_crossings_open, "${graphics_path}/railtypes/metro.png") {
    template_bridge_level_crossings_track(10,630)
    template_bridge_level_crossings_light(10,430)
}
spriteset (spriteset_metro_level_crossings_closed, "${graphics_path}/railtypes/metro.png") {
    template_bridge_level_crossings_track(10,630)
    template_bridge_level_crossings_light(170,430)
}
switch (FEAT_RAILTYPES, SELF, switch_metro_level_crossings, level_crossing_status) {
    LEVEL_CROSSING_CLOSED: spriteset_metro_level_crossings_closed;
    spriteset_metro_level_crossings_open;
}

spriteset (spriteset_metro_depot, "${graphics_path}/railtypes/metro.png") {
    template_depots(10,890)
}

if (param_add_railtypes) {
    if (railtype_available("SAA3") || (loading_stage == LOADING_STAGE_RESERVE)) {
        item (FEAT_RAILTYPES, item_metro) {
            property {
                label:                          "SAA3";
                name:                           string(STR_METROTRACK_NAME);
                menu_text:                      string(STR_METROTRACK_MENU_TEXT);
                build_window_caption:           string(STR_METROTRACK_BUILD_WINDOW_CAPTION);
                autoreplace_text:               string(STR_METROTRACK_AUTOREPLACE_TEXT);
                new_engine_text:                string(STR_METROTRACK_NEW_ENGINE_TEXT);
                railtype_flags:                 bitmask(RAILTYPE_FLAG_NO_LEVEL_CROSSING);
                curve_speed_multiplier:         1;
                station_graphics:               RAILTYPE_STATION_NORMAL;
                speed_limit:                    0;
                acceleration_model:             ACC_MODEL_RAIL;
                //set compatible and powered
                compatible_railtype_list:       ["RAIL", "ELRL",
                                                 "SAAN", "SABN", "SACN", "SADN", "SAEN",
                                                 "SAAE", "SABE", "SACE", "SADE", "SAEE",
                                                 "SAAD", "SABD", "SACD", "SADD", "SAED",
                                                 "SAAd", "SABd", "SACd", "SADd", "SAEd",
                                                 "SAAA", "SABA", "SACA", "SADA", "SAEA",
                                                 "SAAa", "SABa", "SACa", "SADa", "SAEa",
                                                 "SAAZ", "SSAZ", "SUAZ"];
                powered_railtype_list:          ["SAAZ", "SSAZ", "SUAZ"];
                construction_cost:          10;
                map_colour:                 0x25;
                toolbar_caption:            string(STR_METROTRACK_TOOLBAR_CAPTION);
                maintenance_cost:           10;
                alternative_railtype_list:  ["3RDR", "MTRO",
                                             "SAA3", "SAB3", "SAC3", "SAD3", "SAE3"];
            }
            graphics {
                gui:                        spriteset_metro_gui;
                track_overlay:              spriteset_metro_track_overlay;
                underlay:                   spriteset_metro_underlay;
                tunnels:                    spriteset_metro_tunnels;
                //catenary_wire:
                //catenary_pylons
                bridge_surfaces:            spriteset_metro_bridge_surfaces;
                level_crossings:            switch_metro_level_crossings;
                depots:						spriteset_metro_depot;
                //fences:
                //tunnel_overlay:
                signals:                    switch_signals;
            }
        }
    }
}
